(ns task-graph.core-test
  (:require [clojure.test :refer [deftest is testing]]
            [task-graph.core :as core]))

(def catalog
  [{:task/id "A" :task/workflow "wf" :task/requires []}
   {:task/id "G" :task/workflow "wf" :task/requires []}
   {:task/id "E" :task/workflow "wf" :task/requires ["G"]}
   {:task/id "B" :task/workflow "wf" :task/requires ["G"]}])

(deftest ready-tasks-respects-dependencies
  (testing "nothing completed yet: only requirement-free tasks are ready"
    (is (= #{"A" "G"} (set (map :task/id (core/ready-tasks catalog []))))))
  (testing "A completed: G still the only ready task, E/B stay blocked on G"
    (let [ledger [{:task/id "A" :event/seq 1 :event/type :completed}]]
      (is (= #{"G"} (set (map :task/id (core/ready-tasks catalog ledger)))))))
  (testing "A+G completed: E and B become ready"
    (let [ledger [{:task/id "A" :event/seq 1 :event/type :completed}
                  {:task/id "G" :event/seq 2 :event/type :completed}]]
      (is (= #{"E" "B"} (set (map :task/id (core/ready-tasks catalog ledger)))))))
  (testing "a :blocked task never reappears as ready even once its deps clear"
    (let [ledger [{:task/id "A" :event/seq 1 :event/type :completed}
                  {:task/id "G" :event/seq 2 :event/type :completed}
                  {:task/id "B" :event/seq 3 :event/type :blocked}]]
      (is (= #{"E"} (set (map :task/id (core/ready-tasks catalog ledger))))))))

(deftest task-status-uses-latest-event
  (let [ledger [{:task/id "G" :event/seq 1 :event/type :dispatched}
                {:task/id "G" :event/seq 2 :event/type :started}]]
    (is (= :started (core/task-status ledger "G")))))

(deftest workflow-done?-requires-every-task-terminal
  (is (false? (core/workflow-done? catalog [] "wf")))
  (let [ledger (map (fn [id] {:task/id id :event/seq 1 :event/type :completed})
                     ["A" "G" "E" "B"])]
    (is (true? (core/workflow-done? catalog ledger "wf")))))
